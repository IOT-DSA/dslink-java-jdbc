apply plugin: 'application'
apply plugin: 'findbugs'
apply plugin: 'java'
apply plugin: 'idea'

mainClassName = 'org.dsa.iot.jdbc.JdbcDslink'
sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '0.0.3-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

configurations {
    provided
}

idea {
    module {
        scopes.PROVIDED.plus += [ configurations.provided ]
    }
}

dependencies {
    provided 'com.google.code.findbugs:annotations:3.0.0'
    compile 'org.iot-dsa:dslink:0.15.0'
    compile 'org.iot-dsa:commons:0.15.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-dbcp2:2.1.1'
    compile 'mysql:mysql-connector-java:6.0.2'
    compile 'org.postgresql:postgresql:9.4-1206-jdbc41'
    compile 'com.h2database:h2:1.4.192'
    compile 'org.apache.commons:commons-pool2:2.4.2'
}

compileJava {
    sourceSets.main.compileClasspath += configurations.provided
}

run {
    args System.getProperty("exec.args", "").split() + "-d" + "../dslink.json"
    workingDir project.buildDir
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

applicationDistribution.from new File(project.projectDir, "/dslink.json")

startScripts {
    def configureClasspathVar = { findClasspath, pathSeparator, variable, line ->
        line = line.replaceAll(~/^${findClasspath}=.*$/) { original ->
            original += "${pathSeparator}${variable}lib"
        }
    }

    def configureUnixClasspath = configureClasspathVar.curry('CLASSPATH', ':', '\$APP_HOME/*')
    def configureWindowsClasspath = configureClasspathVar.curry('set CLASSPATH', ';', '%APP_HOME%\\*')

    doLast {
        unixScript.text =
                unixScript
                        .readLines()
                        .collect(configureUnixClasspath)
                        .join('\n')

        windowsScript.text =
                windowsScript
                        .readLines()
                        .collect(configureWindowsClasspath)
                        .join('\r\n')

    }
}

apply plugin: 'application'
apply plugin: 'java'

import groovy.json.JsonSlurper
build { //Update version in dslink.json
    def inputFile = file('dslink.json')
    def json = new JsonSlurper().parseText(inputFile.text)
    version = json.version
}

mainClassName = 'org.dsa.iot.jdbc.JdbcDslink'
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
    mavenLocal()
}

wrapper {
    gradleVersion = '6.2.2'
}

dependencies {
    compileOnly 'com.google.code.findbugs:annotations:[3.0.1,)'
    implementation 'com.h2database:h2:1.4.199'
    implementation 'org.apache.commons:commons-dbcp2:2.7.0'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.apache.commons:commons-pool2:2.8.0'
    implementation 'org.postgresql:postgresql:42.2.11.jre7'
    implementation 'mysql:mysql-connector-java:6.0.6'
    implementation 'org.iot-dsa:dslink:0.23.2'
    implementation 'org.iot-dsa:commons:0.23.2'
}

run {
    args System.getProperty("exec.args", "").split() + "-d" + "../dslink.json"
    workingDir project.buildDir
}

applicationDistribution.from new File(project.projectDir, "/dslink.json")

startScripts {
    def configureClasspathVar = { findClasspath, pathSeparator, slash, variable, line ->
        line = line.replaceAll(~/^${findClasspath}=.*$/) { original ->
            original += "${pathSeparator}${variable}lib${slash}*"
        }
    }

    def configureUnixClasspath = configureClasspathVar.curry('CLASSPATH', ':', '/', '\$APP_HOME/')
    def configureWindowsClasspath = configureClasspathVar.curry('set CLASSPATH', ';', '\\', '%APP_HOME%\\')

    doLast {
        unixScript.text =
                unixScript
                        .readLines()
                        .collect(configureUnixClasspath)
                        .join('\n')

        windowsScript.text =
                windowsScript
                        .readLines()
                        .collect(configureWindowsClasspath)
                        .join('\r\n')

    }
}
